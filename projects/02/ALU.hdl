// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero x / negate
    Mux16(a=x, sel=zx, out=zerox);
    Not16(in=zerox, out=notx);
    Mux16(a=zerox, b=notx,sel=nx, out=finalx);
    
    // zero y / negate
    Mux16(a=y, sel=zy, out=zeroy);
    Not16(in=zeroy, out=noty);
    Mux16(a=zeroy, b=noty, sel=ny, out=finaly);
    
    // add or and
    Add16(a=finalx, b=finaly, out=XplusY);
    And16(a=finalx, b=finaly, out=XandY);
    Mux16(a=XandY, b=XplusY, sel=f, out=addOrAnd);
    
    // negate
    Not16(in=addOrAnd, out=notOut);
    Mux16(a=addOrAnd, b=notOut, sel=no, out=out, out[0..7]=finalLeft, out[8..15]=finalRight, out[15]=firstOut);
    
    Or8Way(in=finalLeft, out=half1);
    Or8Way(in=finalRight, out=half2);
    Or(a=half1, b=half2, out=isZero); // 0 if no zero bits in out (0000), 1 otherwise (0100)
    Not(in=isZero, out=zr); // 1 if out is 0
    
    And(a=firstOut, b=true, out=ng); // if firstOut (left most bit that indicates negative) is 1, ng=1
    
}
